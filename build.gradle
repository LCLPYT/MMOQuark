plugins {
	id 'fabric-loom'
	id 'maven-publish'
	id 'gradle-build-utils'
}

def privateProps = loadProperties.call('publish.properties')

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = gitVersion.call()
group = project.maven_group

repositories {
	mavenCentral()

	maven {
		url "https://repo.lclpnet.work/repository/internal"
	}

	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation "work.lclpnet.mods:mmocontent:${project.mmocontent_version}"
	modImplementation "work.lclpnet.mods:mcct:${project.mcct_version}"
	include "work.lclpnet.mods:mcct:${project.mcct_version}"

	if (project.enable_sodium) {
		modImplementation "maven.modrinth:sodium:${project.sodium_version}"
		implementation 'org.joml:joml:1.10.4'
	}

	if (project.enable_iris) {
		modImplementation "maven.modrinth:iris:${project.iris_version}"
		modImplementation "org.anarres:jcpp:${project.jcpp_version}" // Preprocessing library used by Iris
	}

	implementation 'com.google.code.findbugs:jsr305:3.0.2'
}

loom {
	accessWidenerPath = file("src/main/resources/mmoquark.accesswidener")
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"

	// The Minecraft launcher currently installs Java 8 for users, so your mod probably wants to target Java 8 too
	// JDK 9 introduced a new way of specifying this that will make sure no newer classes or methods are used.
	// We'll use that if it's available, but otherwise we'll use the older option.
	def targetVersion = 17
	if (JavaVersion.current().isJava9Compatible()) {
		it.options.release.set(targetVersion)
	}
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task if it is present
	withSourcesJar()
}

jar {
	from("LICENSE.md") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId = project.archives_base_name

			from components.java

			pom {
				name = 'MMOQuark'
				description = 'A lightweight Fabric port of the famous Quark Minecraft modification.'
			}
		}
	}

	repositories {
		maven {
			def env = System.getenv()
			if (Arrays.stream("DEPLOY_URL", "DEPLOY_USER", "DEPLOY_PASSWORD").allMatch(env.&containsKey)) {
				credentials {
					username env.get("DEPLOY_USER")
					password env.get("DEPLOY_PASSWORD")
				}
				url env.get("DEPLOY_URL")
			}
			else if (Arrays.stream("mavenHost", "mavenUser", "mavenPassword").allMatch(privateProps.&containsKey)) {
				credentials {
					username privateProps.getProperty('mavenUser')
					password privateProps.getProperty('mavenPassword')
				}
				url privateProps.getProperty('mavenHost')
			} else {
				url "file:///${project.projectDir}/repo"
			}
		}
	}
}
